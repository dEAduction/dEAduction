/-
Copyright (c) 2019 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import category_theory.comma
import category_theory.groupoid

/-!
# The category of elements

This file defines the category of elements, also known as (a special case of) the Grothendieck construction.

Given a functor `F : C ‚•§ Type`, an object of `F.elements` is a pair `(X : C, x : F.obj X)`.
A morphism `(X, x) ‚ü∂ (Y, y)` is a morphism `f : X ‚ü∂ Y` in `C`, so `F.map f` takes `x` to `y`.

## Implementation notes
This construction is equivalent to a special case of a comma construction, so this is mostly just
a more convenient API. We prove the equivalence in `category_theory.category_of_elements.comma_equivalence`.

## References
* [Emily Riehl, *Category Theory in Context*, Section 2.4][riehl2017]
* <https://en.wikipedia.org/wiki/Category_of_elements>
* <https://ncatlab.org/nlab/show/category+of+elements>

## Tags
category of elements, Grothendieck construction, comma category
-/

namespace category_theory

universes w v u
variables {C : Type u} [ùíû : category.{v} C]
include ùíû

/-- The type of objects for the category of elements of a functor `F : C ‚•§ Type` is a pair `(X : C, x : F.obj X)`. -/
def functor.elements (F : C ‚•§ Type w) := (Œ£ c : C, F.obj c)

/-- The category structure on `F.elements`, for `F : C ‚•§ Type`.
    A morphism `(X, x) ‚ü∂ (Y, y)` is a morphism `f : X ‚ü∂ Y` in `C`, so `F.map f` takes `x` to `y`.
 -/
instance category_of_elements (F : C ‚•§ Type w) : category F.elements :=
{ hom := Œª p q, { f : p.1 ‚ü∂ q.1 // (F.map f) p.2 = q.2 },
  id := Œª p, ‚ü®ùüô p.1, by obviously‚ü©,
  comp := Œª p q r f g, ‚ü®f.val ‚â´ g.val, by obviously‚ü© }

namespace category_of_elements

@[ext]
lemma ext (F : C ‚•§ Type w) {x y : F.elements} (f g : x ‚ü∂ y) (w : f.val = g.val) : f = g :=
subtype.eq' w

@[simp] lemma comp_val {F : C ‚•§ Type w} {p q r : F.elements} {f : p ‚ü∂ q} {g : q ‚ü∂ r} :
  (f ‚â´ g).val = f.val ‚â´ g.val := rfl

@[simp] lemma id_val {F : C ‚•§ Type w} {p : F.elements} : (ùüô p : p ‚ü∂ p).val = ùüô p.1 := rfl

end category_of_elements

omit ùíû -- We'll assume C has a groupoid structure, so temporarily forget its category structure
-- to avoid conflicts.
instance groupoid_of_elements [groupoid C] (F : C ‚•§ Type w) : groupoid F.elements :=
{ inv := Œª p q f, ‚ü®inv f.val,
      calc F.map (inv f.val) q.2 = F.map (inv f.val) (F.map f.val p.2) : by rw f.2
                             ... = (F.map f.val ‚â´ F.map (inv f.val)) p.2 : by simp
                             ... = p.2 : by {rw ‚Üêfunctor.map_comp, simp}‚ü© }
include ùíû

namespace category_of_elements
variable (F : C ‚•§ Type w)

/-- The functor out of the category of elements which forgets the element. -/
def œÄ : F.elements ‚•§ C :=
{ obj := Œª X, X.1,
  map := Œª X Y f, f.val }

@[simp] lemma œÄ_obj (X : F.elements) : (œÄ F).obj X = X.1 := rfl
@[simp] lemma œÄ_map {X Y : F.elements} (f : X ‚ü∂ Y) : (œÄ F).map f = f.val := rfl

/-- The forward direction of the equivalence `F.elements ‚âÖ (*, F)`. -/
def to_comma : F.elements ‚•§ comma ((functor.const punit).obj punit) F :=
{ obj := Œª X, { left := punit.star, right := X.1, hom := Œª _, X.2 },
  map := Œª X Y f, { right := f.val } }

@[simp] lemma to_comma_obj (X) :
  (to_comma F).obj X = { left := punit.star, right := X.1, hom := Œª _, X.2 } := rfl
@[simp] lemma to_comma_map {X Y} (f : X ‚ü∂ Y) :
  (to_comma F).map f = { right := f.val } := rfl

/-- The reverse direction of the equivalence `F.elements ‚âÖ (*, F)`. -/
def from_comma : comma ((functor.const punit).obj punit) F ‚•§ F.elements :=
{ obj := Œª X, ‚ü®X.right, X.hom (punit.star)‚ü©,
  map := Œª X Y f, ‚ü®f.right, congr_fun f.w'.symm punit.star‚ü© }

@[simp] lemma from_comma_obj (X) :
  (from_comma F).obj X = ‚ü®X.right, X.hom (punit.star)‚ü© := rfl
@[simp] lemma from_comma_map {X Y} (f : X ‚ü∂ Y) :
  (from_comma F).map f = ‚ü®f.right, congr_fun f.w'.symm punit.star‚ü© := rfl

/-- The equivalence between the category of elements `F.elements`
    and the comma category `(*, F)`. -/
def comma_equivalence : F.elements ‚âå comma ((functor.const punit).obj punit) F :=
equivalence.mk (to_comma F) (from_comma F)
  (nat_iso.of_components (Œª X, eq_to_iso (by tidy)) (by tidy))
  (nat_iso.of_components
    (Œª X, { hom := { right := ùüô _ }, inv := { right := ùüô _ } })
    (by tidy))

end category_of_elements
end category_theory
