/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Mario Carneiro
-/
import topology.basic

/-!
# Ordering on topologies and (co)induced topologies

Topologies on a fixed type `Œ±` are ordered, by reverse inclusion.
That is, for topologies `t‚ÇÅ` and `t‚ÇÇ` on `Œ±`, we write `t‚ÇÅ ‚â§ t‚ÇÇ`
if every set open in `t‚ÇÇ` is also open in `t‚ÇÅ`.
(One also calls `t‚ÇÅ` finer than `t‚ÇÇ`, and `t‚ÇÇ` coarser than `t‚ÇÅ`.)

Any function `f : Œ± ‚Üí Œ≤` induces
       `induced f : topological_space Œ≤ ‚Üí topological_space Œ±`
and  `coinduced f : topological_space Œ± ‚Üí topological_space Œ≤`.
Continuity, the ordering on topologies and (co)induced topologies are
related as follows:
* The identity map (Œ±, t‚ÇÅ) ‚Üí (Œ±, t‚ÇÇ) is continuous iff t‚ÇÅ ‚â§ t‚ÇÇ.
* A map f : (Œ±, t) ‚Üí (Œ≤, u) is continuous
    iff             t ‚â§ induced f u   (`continuous_iff_le_induced`)
    iff coinduced f t ‚â§ u             (`continuous_iff_coinduced_le`).

Topologies on Œ± form a complete lattice, with ‚ä• the discrete topology
and ‚ä§ the indiscrete topology.

For a function f : Œ± ‚Üí Œ≤, (coinduced f, induced f) is a Galois connection
between topologies on Œ± and topologies on Œ≤.

## Implementation notes

There is a Galois insertion between topologies on Œ± (with the inclusion ordering)
and all collections of sets in Œ±. The complete lattice structure on topologies
on Œ± is defined as the reverse of the one obtained via this Galois insertion.

## Tags

finer, coarser, induced topology, coinduced topology

-/

open set filter classical
open_locale classical topological_space filter

universes u v w

namespace topological_space
variables {Œ± : Type u}

/-- The open sets of the least topology containing a collection of basic sets. -/
inductive generate_open (g : set (set Œ±)) : set Œ± ‚Üí Prop
| basic  : ‚àÄs‚ààg, generate_open s
| univ   : generate_open univ
| inter  : ‚àÄs t, generate_open s ‚Üí generate_open t ‚Üí generate_open (s ‚à© t)
| sUnion : ‚àÄk, (‚àÄs‚ààk, generate_open s) ‚Üí generate_open (‚ãÉ‚ÇÄ k)

/-- The smallest topological space containing the collection `g` of basic sets -/
def generate_from (g : set (set Œ±)) : topological_space Œ± :=
{ is_open        := generate_open g,
  is_open_univ   := generate_open.univ,
  is_open_inter  := generate_open.inter,
  is_open_sUnion := generate_open.sUnion  }

lemma nhds_generate_from {g : set (set Œ±)} {a : Œ±} :
  @nhds Œ± (generate_from g) a = (‚®Ös‚àà{s | a ‚àà s ‚àß s ‚àà g}, ùìü s) :=
by rw nhds_def; exact le_antisymm
  (infi_le_infi $ assume s, infi_le_infi_const $ assume ‚ü®as, sg‚ü©, ‚ü®as, generate_open.basic _ sg‚ü©)
  (le_infi $ assume s, le_infi $ assume ‚ü®as, hs‚ü©,
    begin
      revert as, clear_, induction hs,
      case generate_open.basic : s hs
      { exact assume as, infi_le_of_le s $ infi_le _ ‚ü®as, hs‚ü© },
      case generate_open.univ
      { rw [principal_univ],
        exact assume _, le_top },
      case generate_open.inter : s t hs' ht' hs ht
      { exact assume ‚ü®has, hat‚ü©, calc _ ‚â§ ùìü s ‚äì ùìü t : le_inf (hs has) (ht hat)
          ... = _ : inf_principal },
      case generate_open.sUnion : k hk' hk
      { exact Œª ‚ü®t, htk, hat‚ü©, calc _ ‚â§ ùìü t : hk t htk hat
          ... ‚â§ _ : le_principal_iff.2 $ subset_sUnion_of_mem htk }
    end)

lemma tendsto_nhds_generate_from {Œ≤ : Type*} {m : Œ± ‚Üí Œ≤} {f : filter Œ±} {g : set (set Œ≤)} {b : Œ≤}
  (h : ‚àÄs‚ààg, b ‚àà s ‚Üí m ‚Åª¬π' s ‚àà f) : tendsto m f (@nhds Œ≤ (generate_from g) b) :=
by rw [nhds_generate_from]; exact
  (tendsto_infi.2 $ assume s, tendsto_infi.2 $ assume ‚ü®hbs, hsg‚ü©, tendsto_principal.2 $ h s hsg hbs)

/-- Construct a topology on Œ± given the filter of neighborhoods of each point of Œ±. -/
protected def mk_of_nhds (n : Œ± ‚Üí filter Œ±) : topological_space Œ± :=
{ is_open        := Œªs, ‚àÄa‚ààs, s ‚àà n a,
  is_open_univ   := assume x h, univ_mem_sets,
  is_open_inter  := assume s t hs ht x ‚ü®hxs, hxt‚ü©, inter_mem_sets (hs x hxs) (ht x hxt),
  is_open_sUnion := assume s hs a ‚ü®x, hx, hxa‚ü©, mem_sets_of_superset (hs x hx _ hxa) (set.subset_sUnion_of_mem hx) }

lemma nhds_mk_of_nhds (n : Œ± ‚Üí filter Œ±) (a : Œ±)
  (h‚ÇÄ : pure ‚â§ n) (h‚ÇÅ : ‚àÄ{a s}, s ‚àà n a ‚Üí ‚àÉ t ‚àà n a, t ‚äÜ s ‚àß ‚àÄa' ‚àà t, s ‚àà n a') :
  @nhds Œ± (topological_space.mk_of_nhds n) a = n a :=
begin
  letI := topological_space.mk_of_nhds n,
  refine le_antisymm (assume s hs, _) (assume s hs, _),
  { have h‚ÇÄ : {b | s ‚àà n b} ‚äÜ s := assume b hb, mem_pure_sets.1 $ h‚ÇÄ b hb,
    have h‚ÇÅ : {b | s ‚àà n b} ‚àà ùìù a,
    { refine mem_nhds_sets (assume b (hb : s ‚àà n b), _) hs,
      rcases h‚ÇÅ hb with ‚ü®t, ht, hts, h‚ü©,
      exact mem_sets_of_superset ht h },
    exact mem_sets_of_superset h‚ÇÅ h‚ÇÄ },
  { rcases (@mem_nhds_sets_iff Œ± (topological_space.mk_of_nhds n) _ _).1 hs with ‚ü®t, hts, ht, hat‚ü©,
    exact (n a).sets_of_superset (ht _ hat) hts },
end

end topological_space

section lattice

variables {Œ± : Type u} {Œ≤ : Type v}

/-- The inclusion ordering on topologies on Œ±. We use it to get a complete
   lattice instance via the Galois insertion method, but the partial order
   that we will eventually impose on `topological_space Œ±` is the reverse one. -/
def tmp_order : partial_order (topological_space Œ±) :=
{ le          := Œªt s, t.is_open ‚â§ s.is_open,
  le_antisymm := assume t s h‚ÇÅ h‚ÇÇ, topological_space_eq $ le_antisymm h‚ÇÅ h‚ÇÇ,
  le_refl     := assume t, le_refl t.is_open,
  le_trans    := assume a b c h‚ÇÅ h‚ÇÇ, @le_trans _ _ a.is_open b.is_open c.is_open h‚ÇÅ h‚ÇÇ }

local attribute [instance] tmp_order

/- We'll later restate this lemma in terms of the correct order on `topological_space Œ±`. -/
private lemma generate_from_le_iff_subset_is_open {g : set (set Œ±)} {t : topological_space Œ±} :
  topological_space.generate_from g ‚â§ t ‚Üî g ‚äÜ {s | t.is_open s} :=
iff.intro
  (assume ht s hs, ht _ $ topological_space.generate_open.basic s hs)
  (assume hg s hs, hs.rec_on (assume v hv, hg hv)
    t.is_open_univ (assume u v _ _, t.is_open_inter u v) (assume k _, t.is_open_sUnion k))

/-- If `s` equals the collection of open sets in the topology it generates,
  then `s` defines a topology. -/
protected def mk_of_closure (s : set (set Œ±))
  (hs : {u | (topological_space.generate_from s).is_open u} = s) : topological_space Œ± :=
{ is_open        := Œªu, u ‚àà s,
  is_open_univ   := hs ‚ñ∏ topological_space.generate_open.univ,
  is_open_inter  := hs ‚ñ∏ topological_space.generate_open.inter,
  is_open_sUnion := hs ‚ñ∏ topological_space.generate_open.sUnion }

lemma mk_of_closure_sets {s : set (set Œ±)}
  {hs : {u | (topological_space.generate_from s).is_open u} = s} :
  mk_of_closure s hs = topological_space.generate_from s :=
topological_space_eq hs.symm

/-- The Galois insertion between `set (set Œ±)` and `topological_space Œ±` whose lower part
  sends a collection of subsets of Œ± to the topology they generate, and whose upper part
  sends a topology to its collection of open subsets. -/
def gi_generate_from (Œ± : Type*) :
  galois_insertion topological_space.generate_from (Œªt:topological_space Œ±, {s | t.is_open s}) :=
{ gc        := assume g t, generate_from_le_iff_subset_is_open,
  le_l_u    := assume ts s hs, topological_space.generate_open.basic s hs,
  choice    := Œªg hg, mk_of_closure g
    (subset.antisymm hg $ generate_from_le_iff_subset_is_open.1 $ le_refl _),
  choice_eq := assume s hs, mk_of_closure_sets }

lemma generate_from_mono {Œ±} {g‚ÇÅ g‚ÇÇ : set (set Œ±)} (h : g‚ÇÅ ‚äÜ g‚ÇÇ) :
  topological_space.generate_from g‚ÇÅ ‚â§ topological_space.generate_from g‚ÇÇ :=
(gi_generate_from _).gc.monotone_l h

/-- The complete lattice of topological spaces, but built on the inclusion ordering. -/
def tmp_complete_lattice {Œ± : Type u} : complete_lattice (topological_space Œ±) :=
(gi_generate_from Œ±).lift_complete_lattice


/-- The ordering on topologies on the type `Œ±`.
  `t ‚â§ s` if every set open in `s` is also open in `t` (`t` is finer than `s`). -/
instance : partial_order (topological_space Œ±) :=
{ le          := Œª t s, s.is_open ‚â§ t.is_open,
  le_antisymm := assume t s h‚ÇÅ h‚ÇÇ, topological_space_eq $ le_antisymm h‚ÇÇ h‚ÇÅ,
  le_refl     := assume t, le_refl t.is_open,
  le_trans    := assume a b c h‚ÇÅ h‚ÇÇ, le_trans h‚ÇÇ h‚ÇÅ }

lemma le_generate_from_iff_subset_is_open {g : set (set Œ±)} {t : topological_space Œ±} :
  t ‚â§ topological_space.generate_from g ‚Üî g ‚äÜ {s | t.is_open s} :=
generate_from_le_iff_subset_is_open

/-- Topologies on `Œ±` form a complete lattice, with `‚ä•` the discrete topology
  and `‚ä§` the indiscrete topology. The infimum of a collection of topologies
  is the topology generated by all their open sets, while the supremem is the
  topology whose open sets are those sets open in every member of the collection. -/
instance : complete_lattice (topological_space Œ±) :=
@order_dual.complete_lattice _ tmp_complete_lattice

/-- A topological space is discrete if every set is open, that is,
  its topology equals the discrete topology `‚ä•`. -/
class discrete_topology (Œ± : Type*) [t : topological_space Œ±] : Prop :=
(eq_bot [] : t = ‚ä•)

@[simp] lemma is_open_discrete [topological_space Œ±] [discrete_topology Œ±] (s : set Œ±) :
  is_open s :=
(discrete_topology.eq_bot Œ±).symm ‚ñ∏ trivial

@[simp] lemma is_closed_discrete [topological_space Œ±] [discrete_topology Œ±] (s : set Œ±) :
  is_closed s :=
(discrete_topology.eq_bot Œ±).symm ‚ñ∏ trivial

lemma continuous_of_discrete_topology [topological_space Œ±] [discrete_topology Œ±]
  [topological_space Œ≤] {f : Œ± ‚Üí Œ≤} : continuous f :=
Œªs hs, is_open_discrete _

lemma nhds_bot (Œ± : Type*) : (@nhds Œ± ‚ä•) = pure :=
begin
  refine le_antisymm _ (@pure_le_nhds Œ± ‚ä•),
  assume a s hs,
  exact @mem_nhds_sets Œ± ‚ä• a s trivial hs
end

lemma nhds_discrete (Œ± : Type*) [topological_space Œ±] [discrete_topology Œ±] : (@nhds Œ± _) = pure :=
(discrete_topology.eq_bot Œ±).symm ‚ñ∏ nhds_bot Œ±

lemma le_of_nhds_le_nhds {t‚ÇÅ t‚ÇÇ : topological_space Œ±} (h : ‚àÄx, @nhds Œ± t‚ÇÅ x ‚â§ @nhds Œ± t‚ÇÇ x) :
  t‚ÇÅ ‚â§ t‚ÇÇ :=
assume s, show @is_open Œ± t‚ÇÇ s ‚Üí @is_open Œ± t‚ÇÅ s,
  by { simp only [is_open_iff_nhds, le_principal_iff],  exact assume hs a ha, h _ $ hs _ ha }

lemma eq_of_nhds_eq_nhds {t‚ÇÅ t‚ÇÇ : topological_space Œ±} (h : ‚àÄx, @nhds Œ± t‚ÇÅ x = @nhds Œ± t‚ÇÇ x) :
  t‚ÇÅ = t‚ÇÇ :=
le_antisymm
  (le_of_nhds_le_nhds $ assume x, le_of_eq $ h x)
  (le_of_nhds_le_nhds $ assume x, le_of_eq $ (h x).symm)

lemma eq_bot_of_singletons_open {t : topological_space Œ±} (h : ‚àÄ x, t.is_open {x}) : t = ‚ä• :=
bot_unique $ Œª s hs, bUnion_of_singleton s ‚ñ∏ is_open_bUnion (Œª x _, h x)

end lattice

section galois_connection
variables {Œ± : Type*} {Œ≤ : Type*} {Œ≥ : Type*}

/-- Given `f : Œ± ‚Üí Œ≤` and a topology on `Œ≤`, the induced topology on `Œ±` is the collection of
  sets that are preimages of some open set in `Œ≤`. This is the coarsest topology that
  makes `f` continuous. -/
def topological_space.induced {Œ± : Type u} {Œ≤ : Type v} (f : Œ± ‚Üí Œ≤) (t : topological_space Œ≤) :
  topological_space Œ± :=
{ is_open        := Œªs, ‚àÉs', t.is_open s' ‚àß f ‚Åª¬π' s' = s,
  is_open_univ   := ‚ü®univ, t.is_open_univ, preimage_univ‚ü©,
  is_open_inter  := by rintro s‚ÇÅ s‚ÇÇ ‚ü®s'‚ÇÅ, hs‚ÇÅ, rfl‚ü© ‚ü®s'‚ÇÇ, hs‚ÇÇ, rfl‚ü©;
    exact ‚ü®s'‚ÇÅ ‚à© s'‚ÇÇ, t.is_open_inter _ _ hs‚ÇÅ hs‚ÇÇ, preimage_inter‚ü©,
  is_open_sUnion := assume s h,
  begin
    simp only [classical.skolem] at h,
    cases h with f hf,
    apply exists.intro (‚ãÉ(x : set Œ±) (h : x ‚àà s), f x h),
    simp only [sUnion_eq_bUnion, preimage_Union, (Œªx h, (hf x h).right)], refine ‚ü®_, rfl‚ü©,
    exact (@is_open_Union Œ≤ _ t _ $ assume i,
      show is_open (‚ãÉh, f i h), from @is_open_Union Œ≤ _ t _ $ assume h, (hf i h).left)
  end }

lemma is_open_induced_iff [t : topological_space Œ≤] {s : set Œ±} {f : Œ± ‚Üí Œ≤} :
  @is_open Œ± (t.induced f) s ‚Üî (‚àÉt, is_open t ‚àß f ‚Åª¬π' t = s) :=
iff.rfl

lemma is_closed_induced_iff [t : topological_space Œ≤] {s : set Œ±} {f : Œ± ‚Üí Œ≤} :
  @is_closed Œ± (t.induced f) s ‚Üî (‚àÉt, is_closed t ‚àß s = f ‚Åª¬π' t) :=
‚ü®assume ‚ü®t, ht, heq‚ü©, ‚ü®-t, is_closed_compl_iff.2 ht,
    by simp only [preimage_compl, heq, compl_compl]‚ü©,
  assume ‚ü®t, ht, heq‚ü©, ‚ü®-t, ht, by simp only [preimage_compl, heq.symm]‚ü©‚ü©

/-- Given `f : Œ± ‚Üí Œ≤` and a topology on `Œ±`, the coinduced topology on `Œ≤` is defined
  such that `s:set Œ≤` is open if the preimage of `s` is open. This is the finest topology that
  makes `f` continuous. -/
def topological_space.coinduced {Œ± : Type u} {Œ≤ : Type v} (f : Œ± ‚Üí Œ≤) (t : topological_space Œ±) :
  topological_space Œ≤ :=
{ is_open        := Œªs, t.is_open (f ‚Åª¬π' s),
  is_open_univ   := by rw preimage_univ; exact t.is_open_univ,
  is_open_inter  := assume s‚ÇÅ s‚ÇÇ h‚ÇÅ h‚ÇÇ, by rw preimage_inter; exact t.is_open_inter _ _ h‚ÇÅ h‚ÇÇ,
  is_open_sUnion := assume s h, by rw [preimage_sUnion]; exact (@is_open_Union _ _ t _ $ assume i,
    show is_open (‚ãÉ (H : i ‚àà s), f ‚Åª¬π' i), from
      @is_open_Union _ _ t _ $ assume hi, h i hi) }

lemma is_open_coinduced {t : topological_space Œ±} {s : set Œ≤} {f : Œ± ‚Üí Œ≤} :
  @is_open Œ≤ (topological_space.coinduced f t) s ‚Üî is_open (f ‚Åª¬π' s) :=
iff.rfl

variables {t t‚ÇÅ t‚ÇÇ : topological_space Œ±} {t' : topological_space Œ≤} {f : Œ± ‚Üí Œ≤} {g : Œ≤ ‚Üí Œ±}

lemma coinduced_le_iff_le_induced {f : Œ± ‚Üí Œ≤ } {tŒ± : topological_space Œ±} {tŒ≤ : topological_space Œ≤} :
  tŒ±.coinduced f ‚â§ tŒ≤ ‚Üî tŒ± ‚â§ tŒ≤.induced f :=
iff.intro
  (assume h s ‚ü®t, ht, hst‚ü©, hst ‚ñ∏ h _ ht)
  (assume h s hs, show tŒ±.is_open (f ‚Åª¬π' s), from h _ ‚ü®s, hs, rfl‚ü©)

lemma gc_coinduced_induced (f : Œ± ‚Üí Œ≤) :
  galois_connection (topological_space.coinduced f) (topological_space.induced f) :=
assume f g, coinduced_le_iff_le_induced

lemma induced_mono (h : t‚ÇÅ ‚â§ t‚ÇÇ) : t‚ÇÅ.induced g ‚â§ t‚ÇÇ.induced g :=
(gc_coinduced_induced g).monotone_u h

lemma coinduced_mono (h : t‚ÇÅ ‚â§ t‚ÇÇ) : t‚ÇÅ.coinduced f ‚â§ t‚ÇÇ.coinduced f :=
(gc_coinduced_induced f).monotone_l h

@[simp] lemma induced_top : (‚ä§ : topological_space Œ±).induced g = ‚ä§ :=
(gc_coinduced_induced g).u_top

@[simp] lemma induced_inf : (t‚ÇÅ ‚äì t‚ÇÇ).induced g = t‚ÇÅ.induced g ‚äì t‚ÇÇ.induced g :=
(gc_coinduced_induced g).u_inf

@[simp] lemma induced_infi {Œπ : Sort w} {t : Œπ ‚Üí topological_space Œ±} :
  (‚®Öi, t i).induced g = (‚®Öi, (t i).induced g) :=
(gc_coinduced_induced g).u_infi

@[simp] lemma coinduced_bot : (‚ä• : topological_space Œ±).coinduced f = ‚ä• :=
(gc_coinduced_induced f).l_bot

@[simp] lemma coinduced_sup : (t‚ÇÅ ‚äî t‚ÇÇ).coinduced f = t‚ÇÅ.coinduced f ‚äî t‚ÇÇ.coinduced f :=
(gc_coinduced_induced f).l_sup

@[simp] lemma coinduced_supr {Œπ : Sort w} {t : Œπ ‚Üí topological_space Œ±} :
  (‚®Üi, t i).coinduced f = (‚®Üi, (t i).coinduced f) :=
(gc_coinduced_induced f).l_supr

lemma induced_id [t : topological_space Œ±] : t.induced id = t :=
topological_space_eq $ funext $ assume s, propext $
  ‚ü®assume ‚ü®s', hs, h‚ü©, h ‚ñ∏ hs, assume hs, ‚ü®s, hs, rfl‚ü©‚ü©

lemma induced_compose [tŒ≥ : topological_space Œ≥]
  {f : Œ± ‚Üí Œ≤} {g : Œ≤ ‚Üí Œ≥} : (tŒ≥.induced g).induced f = tŒ≥.induced (g ‚àò f) :=
topological_space_eq $ funext $ assume s, propext $
  ‚ü®assume ‚ü®s', ‚ü®s, hs, h‚ÇÇ‚ü©, h‚ÇÅ‚ü©, h‚ÇÅ ‚ñ∏ h‚ÇÇ ‚ñ∏ ‚ü®s, hs, rfl‚ü©,
    assume ‚ü®s, hs, h‚ü©, ‚ü®preimage g s, ‚ü®s, hs, rfl‚ü©, h ‚ñ∏ rfl‚ü©‚ü©

lemma coinduced_id [t : topological_space Œ±] : t.coinduced id = t :=
topological_space_eq rfl

lemma coinduced_compose [tŒ± : topological_space Œ±]
  {f : Œ± ‚Üí Œ≤} {g : Œ≤ ‚Üí Œ≥} : (tŒ±.coinduced f).coinduced g = tŒ±.coinduced (g ‚àò f) :=
topological_space_eq rfl

end galois_connection

/- constructions using the complete lattice structure -/
section constructions
open topological_space

variables {Œ± : Type u} {Œ≤ : Type v}

instance inhabited_topological_space {Œ± : Type u} : inhabited (topological_space Œ±) :=
‚ü®‚ä§‚ü©

@[priority 100]
instance subsingleton.discrete_topology [topological_space Œ±] [subsingleton Œ±] :
  discrete_topology Œ± :=
‚ü®eq_bot_of_singletons_open $ Œª x, subsingleton.set_cases is_open_empty is_open_univ ({x} : set Œ±)‚ü©

instance : topological_space empty := ‚ä•
instance : discrete_topology empty := ‚ü®rfl‚ü©
instance : topological_space unit := ‚ä•
instance : discrete_topology unit := ‚ü®rfl‚ü©
instance : topological_space bool := ‚ä•
instance : discrete_topology bool := ‚ü®rfl‚ü©
instance : topological_space ‚Ñï := ‚ä•
instance : discrete_topology ‚Ñï := ‚ü®rfl‚ü©
instance : topological_space ‚Ñ§ := ‚ä•
instance : discrete_topology ‚Ñ§ := ‚ü®rfl‚ü©

instance sierpinski_space : topological_space Prop :=
generate_from {{true}}

lemma le_generate_from {t : topological_space Œ±} { g : set (set Œ±) } (h : ‚àÄs‚ààg, is_open s) :
  t ‚â§ generate_from g :=
le_generate_from_iff_subset_is_open.2 h

lemma induced_generate_from_eq {Œ± Œ≤} {b : set (set Œ≤)} {f : Œ± ‚Üí Œ≤} :
  (generate_from b).induced f = topological_space.generate_from (preimage f '' b) :=
le_antisymm
  (le_generate_from $ ball_image_iff.2 $ assume s hs, ‚ü®s, generate_open.basic _ hs, rfl‚ü©)
  (coinduced_le_iff_le_induced.1 $ le_generate_from $ assume s hs,
    generate_open.basic _ $ mem_image_of_mem _ hs)

/-- This construction is left adjoint to the operation sending a topology on `Œ±`
  to its neighborhood filter at a fixed point `a : Œ±`. -/
protected def topological_space.nhds_adjoint (a : Œ±) (f : filter Œ±) : topological_space Œ± :=
{ is_open        := Œªs, a ‚àà s ‚Üí s ‚àà f,
  is_open_univ   := assume s, univ_mem_sets,
  is_open_inter  := assume s t hs ht ‚ü®has, hat‚ü©, inter_mem_sets (hs has) (ht hat),
  is_open_sUnion := assume k hk ‚ü®u, hu, hau‚ü©, mem_sets_of_superset (hk u hu hau) (subset_sUnion_of_mem hu) }

lemma gc_nhds (a : Œ±) :
  galois_connection  (topological_space.nhds_adjoint a) (Œªt, @nhds Œ± t a) :=
assume f t, by { rw le_nhds_iff, exact ‚ü®Œª H s hs has, H _ has hs, Œª H s has hs, H _ hs has‚ü© }

lemma nhds_mono {t‚ÇÅ t‚ÇÇ : topological_space Œ±} {a : Œ±} (h : t‚ÇÅ ‚â§ t‚ÇÇ) :
  @nhds Œ± t‚ÇÅ a ‚â§ @nhds Œ± t‚ÇÇ a := (gc_nhds a).monotone_u h

lemma nhds_infi {Œπ : Sort*} {t : Œπ ‚Üí topological_space Œ±} {a : Œ±} :
  @nhds Œ± (infi t) a = (‚®Öi, @nhds Œ± (t i) a) := (gc_nhds a).u_infi

lemma nhds_Inf {s : set (topological_space Œ±)} {a : Œ±} :
  @nhds Œ± (Inf s) a = (‚®Öt‚ààs, @nhds Œ± t a) := (gc_nhds a).u_Inf

lemma nhds_inf {t‚ÇÅ t‚ÇÇ : topological_space Œ±} {a : Œ±} :
  @nhds Œ± (t‚ÇÅ ‚äì t‚ÇÇ) a = @nhds Œ± t‚ÇÅ a ‚äì @nhds Œ± t‚ÇÇ a := (gc_nhds a).u_inf

lemma nhds_top {a : Œ±} : @nhds Œ± ‚ä§ a = ‚ä§ := (gc_nhds a).u_top

local notation `cont` := @continuous _ _
local notation `tspace` := topological_space
open topological_space

variables {Œ≥ : Type*} {f : Œ± ‚Üí Œ≤} {Œπ : Sort*}

lemma continuous_iff_coinduced_le {t‚ÇÅ : tspace Œ±} {t‚ÇÇ : tspace Œ≤} :
  cont t‚ÇÅ t‚ÇÇ f ‚Üî coinduced f t‚ÇÅ ‚â§ t‚ÇÇ := iff.rfl

lemma continuous_iff_le_induced {t‚ÇÅ : tspace Œ±} {t‚ÇÇ : tspace Œ≤} :
  cont t‚ÇÅ t‚ÇÇ f ‚Üî t‚ÇÅ ‚â§ induced f t‚ÇÇ :=
iff.trans continuous_iff_coinduced_le (gc_coinduced_induced f _ _)

theorem continuous_generated_from {t : tspace Œ±} {b : set (set Œ≤)}
  (h : ‚àÄs‚ààb, is_open (f ‚Åª¬π' s)) : cont t (generate_from b) f :=
continuous_iff_coinduced_le.2 $ le_generate_from h

lemma continuous_induced_dom {t : tspace Œ≤} : cont (induced f t) t f :=
assume s h, ‚ü®_, h, rfl‚ü©

lemma continuous_induced_rng {g : Œ≥ ‚Üí Œ±} {t‚ÇÇ : tspace Œ≤} {t‚ÇÅ : tspace Œ≥}
  (h : cont t‚ÇÅ t‚ÇÇ (f ‚àò g)) : cont t‚ÇÅ (induced f t‚ÇÇ) g :=
assume s ‚ü®t, ht, s_eq‚ü©, s_eq ‚ñ∏ h t ht

lemma continuous_coinduced_rng {t : tspace Œ±} : cont t (coinduced f t) f :=
assume s h, h

lemma continuous_coinduced_dom {g : Œ≤ ‚Üí Œ≥} {t‚ÇÅ : tspace Œ±} {t‚ÇÇ : tspace Œ≥}
  (h : cont t‚ÇÅ t‚ÇÇ (g ‚àò f)) : cont (coinduced f t‚ÇÅ) t‚ÇÇ g :=
assume s hs, h s hs

lemma continuous_le_dom {t‚ÇÅ t‚ÇÇ : tspace Œ±} {t‚ÇÉ : tspace Œ≤}
  (h‚ÇÅ : t‚ÇÇ ‚â§ t‚ÇÅ) (h‚ÇÇ : cont t‚ÇÅ t‚ÇÉ f) : cont t‚ÇÇ t‚ÇÉ f :=
assume s h, h‚ÇÅ _ (h‚ÇÇ s h)

lemma continuous_le_rng {t‚ÇÅ : tspace Œ±} {t‚ÇÇ t‚ÇÉ : tspace Œ≤}
  (h‚ÇÅ : t‚ÇÇ ‚â§ t‚ÇÉ) (h‚ÇÇ : cont t‚ÇÅ t‚ÇÇ f) : cont t‚ÇÅ t‚ÇÉ f :=
assume s h, h‚ÇÇ s (h‚ÇÅ s h)

lemma continuous_sup_dom {t‚ÇÅ t‚ÇÇ : tspace Œ±} {t‚ÇÉ : tspace Œ≤}
  (h‚ÇÅ : cont t‚ÇÅ t‚ÇÉ f) (h‚ÇÇ : cont t‚ÇÇ t‚ÇÉ f) : cont (t‚ÇÅ ‚äî t‚ÇÇ) t‚ÇÉ f :=
assume s h, ‚ü®h‚ÇÅ s h, h‚ÇÇ s h‚ü©

lemma continuous_sup_rng_left {t‚ÇÅ : tspace Œ±} {t‚ÇÉ t‚ÇÇ : tspace Œ≤} :
  cont t‚ÇÅ t‚ÇÇ f ‚Üí cont t‚ÇÅ (t‚ÇÇ ‚äî t‚ÇÉ) f :=
continuous_le_rng le_sup_left

lemma continuous_sup_rng_right {t‚ÇÅ : tspace Œ±} {t‚ÇÉ t‚ÇÇ : tspace Œ≤} :
  cont t‚ÇÅ t‚ÇÉ f ‚Üí cont t‚ÇÅ (t‚ÇÇ ‚äî t‚ÇÉ) f :=
continuous_le_rng le_sup_right

lemma continuous_Sup_dom {t‚ÇÅ : set (tspace Œ±)} {t‚ÇÇ : tspace Œ≤}
  (h : ‚àÄt‚ààt‚ÇÅ, cont t t‚ÇÇ f) : cont (Sup t‚ÇÅ) t‚ÇÇ f :=
continuous_iff_le_induced.2 $ Sup_le $ assume t ht, continuous_iff_le_induced.1 $ h t ht

lemma continuous_Sup_rng {t‚ÇÅ : tspace Œ±} {t‚ÇÇ : set (tspace Œ≤)} {t : tspace Œ≤}
  (h‚ÇÅ : t ‚àà t‚ÇÇ) (hf : cont t‚ÇÅ t f) : cont t‚ÇÅ (Sup t‚ÇÇ) f :=
continuous_iff_coinduced_le.2 $ le_Sup_of_le h‚ÇÅ $ continuous_iff_coinduced_le.1 hf

lemma continuous_supr_dom {t‚ÇÅ : Œπ ‚Üí tspace Œ±} {t‚ÇÇ : tspace Œ≤}
  (h : ‚àÄi, cont (t‚ÇÅ i) t‚ÇÇ f) : cont (supr t‚ÇÅ) t‚ÇÇ f :=
continuous_Sup_dom $ assume t ‚ü®i, (t_eq : t‚ÇÅ i = t)‚ü©, t_eq ‚ñ∏ h i

lemma continuous_supr_rng {t‚ÇÅ : tspace Œ±} {t‚ÇÇ : Œπ ‚Üí tspace Œ≤} {i : Œπ}
  (h : cont t‚ÇÅ (t‚ÇÇ i) f) : cont t‚ÇÅ (supr t‚ÇÇ) f :=
continuous_Sup_rng ‚ü®i, rfl‚ü© h

lemma continuous_inf_rng {t‚ÇÅ : tspace Œ±} {t‚ÇÇ t‚ÇÉ : tspace Œ≤}
  (h‚ÇÅ : cont t‚ÇÅ t‚ÇÇ f) (h‚ÇÇ : cont t‚ÇÅ t‚ÇÉ f) : cont t‚ÇÅ (t‚ÇÇ ‚äì t‚ÇÉ) f :=
continuous_iff_coinduced_le.2 $ le_inf
  (continuous_iff_coinduced_le.1 h‚ÇÅ)
  (continuous_iff_coinduced_le.1 h‚ÇÇ)

lemma continuous_inf_dom_left {t‚ÇÅ t‚ÇÇ : tspace Œ±} {t‚ÇÉ : tspace Œ≤} :
  cont t‚ÇÅ t‚ÇÉ f ‚Üí cont (t‚ÇÅ ‚äì t‚ÇÇ) t‚ÇÉ f :=
continuous_le_dom inf_le_left

lemma continuous_inf_dom_right {t‚ÇÅ t‚ÇÇ : tspace Œ±} {t‚ÇÉ : tspace Œ≤} :
  cont t‚ÇÇ t‚ÇÉ f ‚Üí cont (t‚ÇÅ ‚äì t‚ÇÇ) t‚ÇÉ f :=
continuous_le_dom inf_le_right

lemma continuous_Inf_dom {t‚ÇÅ : set (tspace Œ±)} {t‚ÇÇ : tspace Œ≤} {t : tspace Œ±} (h‚ÇÅ : t ‚àà t‚ÇÅ) :
  cont t t‚ÇÇ f ‚Üí cont (Inf t‚ÇÅ) t‚ÇÇ f :=
continuous_le_dom $ Inf_le h‚ÇÅ

lemma continuous_Inf_rng {t‚ÇÅ : tspace Œ±} {t‚ÇÇ : set (tspace Œ≤)}
  (h : ‚àÄt‚ààt‚ÇÇ, cont t‚ÇÅ t f) : cont t‚ÇÅ (Inf t‚ÇÇ) f :=
continuous_iff_coinduced_le.2 $ le_Inf $ assume b hb, continuous_iff_coinduced_le.1 $ h b hb

lemma continuous_infi_dom {t‚ÇÅ : Œπ ‚Üí tspace Œ±} {t‚ÇÇ : tspace Œ≤} {i : Œπ} :
  cont (t‚ÇÅ i) t‚ÇÇ f ‚Üí cont (infi t‚ÇÅ) t‚ÇÇ f :=
continuous_le_dom $ infi_le _ _

lemma continuous_infi_rng {t‚ÇÅ : tspace Œ±} {t‚ÇÇ : Œπ ‚Üí tspace Œ≤}
  (h : ‚àÄi, cont t‚ÇÅ (t‚ÇÇ i) f) : cont t‚ÇÅ (infi t‚ÇÇ) f :=
continuous_iff_coinduced_le.2 $ le_infi $ assume i, continuous_iff_coinduced_le.1 $ h i

lemma continuous_bot {t : tspace Œ≤} : cont ‚ä• t f :=
continuous_iff_le_induced.2 $ bot_le

lemma continuous_top {t : tspace Œ±} : cont t ‚ä§ f :=
continuous_iff_coinduced_le.2 $ le_top

/- ùìù in the induced topology -/

theorem mem_nhds_induced [T : topological_space Œ±] (f : Œ≤ ‚Üí Œ±) (a : Œ≤) (s : set Œ≤) :
  s ‚àà @nhds Œ≤ (topological_space.induced f T) a ‚Üî ‚àÉ u ‚àà ùìù (f a), f ‚Åª¬π' u ‚äÜ s :=
begin
  simp only [mem_nhds_sets_iff, is_open_induced_iff, exists_prop, set.mem_set_of_eq],
  split,
  { rintros ‚ü®u, usub, ‚ü®v, openv, ueq‚ü©, au‚ü©,
    exact ‚ü®v, ‚ü®v, set.subset.refl v, openv, by rwa ‚Üêueq at au‚ü©, by rw ueq; exact usub‚ü© },
  rintros ‚ü®u, ‚ü®v, vsubu, openv, amem‚ü©, finvsub‚ü©,
  exact ‚ü®f ‚Åª¬π' v, set.subset.trans (set.preimage_mono vsubu) finvsub, ‚ü®‚ü®v, openv, rfl‚ü©, amem‚ü©‚ü©
end

theorem nhds_induced [T : topological_space Œ±] (f : Œ≤ ‚Üí Œ±) (a : Œ≤) :
  @nhds Œ≤ (topological_space.induced f T) a = comap f (ùìù (f a)) :=
filter_eq $ by ext s; rw mem_nhds_induced; rw mem_comap_sets

lemma induced_iff_nhds_eq [tŒ± : topological_space Œ±] [tŒ≤ : topological_space Œ≤] (f : Œ≤ ‚Üí Œ±) :
tŒ≤ = tŒ±.induced f ‚Üî ‚àÄ b, ùìù b = comap f (ùìù $ f b) :=
‚ü®Œª h a, h.symm ‚ñ∏ nhds_induced f a, Œª h, eq_of_nhds_eq_nhds $ Œª x, by rw [h, nhds_induced]‚ü©

theorem map_nhds_induced_of_surjective [T : topological_space Œ±]
    {f : Œ≤ ‚Üí Œ±} (hf : function.surjective f) (a : Œ≤) :
  map f (@nhds Œ≤ (topological_space.induced f T) a) = ùìù (f a) :=
by rw [nhds_induced, map_comap_of_surjective hf]

end constructions

section induced
open topological_space
variables {Œ± : Type*} {Œ≤ : Type*}
variables [t : topological_space Œ≤] {f : Œ± ‚Üí Œ≤}

theorem is_open_induced_eq {s : set Œ±} :
  @is_open _ (induced f t) s ‚Üî s ‚àà preimage f '' {s | is_open s} :=
iff.rfl

theorem is_open_induced {s : set Œ≤} (h : is_open s) : (induced f t).is_open (f ‚Åª¬π' s) :=
‚ü®s, h, rfl‚ü©

lemma map_nhds_induced_eq {a : Œ±} (h : range f ‚àà ùìù (f a)) :
  map f (@nhds Œ± (induced f t) a) = ùìù (f a) :=
by rw [nhds_induced, filter.map_comap h]

lemma closure_induced [t : topological_space Œ≤] {f : Œ± ‚Üí Œ≤} {a : Œ±} {s : set Œ±}
  (hf : ‚àÄx y, f x = f y ‚Üí x = y) :
  a ‚àà @closure Œ± (topological_space.induced f t) s ‚Üî f a ‚àà closure (f '' s) :=
have comap f (ùìù (f a) ‚äì ùìü (f '' s)) ‚â† ‚ä• ‚Üî ùìù (f a) ‚äì ùìü (f '' s) ‚â† ‚ä•,
  from ‚ü®assume h‚ÇÅ h‚ÇÇ, h‚ÇÅ $ h‚ÇÇ.symm ‚ñ∏ comap_bot,
    assume h,
    forall_sets_nonempty_iff_ne_bot.mp $
      assume s‚ÇÅ ‚ü®s‚ÇÇ, hs‚ÇÇ, (hs : f ‚Åª¬π' s‚ÇÇ ‚äÜ s‚ÇÅ)‚ü©,
      have f '' s ‚àà ùìù (f a) ‚äì ùìü (f '' s),
        from mem_inf_sets_of_right $ by simp [subset.refl],
      have s‚ÇÇ ‚à© f '' s ‚àà ùìù (f a) ‚äì ùìü (f '' s),
        from inter_mem_sets hs‚ÇÇ this,
      let ‚ü®b, hb‚ÇÅ, ‚ü®a, ha, ha‚ÇÇ‚ü©‚ü© := nonempty_of_mem_sets h this in
      ‚ü®_, hs $ by rwa [‚Üêha‚ÇÇ] at hb‚ÇÅ‚ü©‚ü©,
calc a ‚àà @closure Œ± (topological_space.induced f t) s
    ‚Üî (@nhds Œ± (topological_space.induced f t) a) ‚äì ùìü s ‚â† ‚ä• : by rw [closure_eq_cluster_pts]; refl
  ... ‚Üî comap f (ùìù (f a)) ‚äì ùìü (f ‚Åª¬π' (f '' s)) ‚â† ‚ä• : by rw [nhds_induced, preimage_image_eq _ hf]
  ... ‚Üî comap f (ùìù (f a) ‚äì ùìü (f '' s)) ‚â† ‚ä• : by rw [comap_inf, ‚Üêcomap_principal]
  ... ‚Üî _ : by rwa [closure_eq_cluster_pts]

end induced

section sierpinski
variables {Œ± : Type*} [topological_space Œ±]

@[simp] lemma is_open_singleton_true : is_open ({true} : set Prop) :=
topological_space.generate_open.basic _ (by simp)

lemma continuous_Prop {p : Œ± ‚Üí Prop} : continuous p ‚Üî is_open {x | p x} :=
‚ü®assume h : continuous p,
  have is_open (p ‚Åª¬π' {true}),
    from h _ is_open_singleton_true,
  by simp [preimage, eq_true] at this; assumption,
  assume h : is_open {x | p x},
  continuous_generated_from $ assume s (hs : s ‚àà {{true}}),
    by simp at hs; simp [hs, preimage, eq_true, h]‚ü©

end sierpinski

section infi
variables {Œ± : Type u} {Œπ : Type v} {t : Œπ ‚Üí topological_space Œ±}

lemma is_open_supr_iff {s : set Œ±} : @is_open _ (‚®Ü i, t i) s ‚Üî ‚àÄ i, @is_open _ (t i) s :=
begin
  -- s defines a map from Œ± to Prop, which is continuous iff s is open.
  suffices : @continuous _ _ (‚®Ü i, t i) _ s ‚Üî ‚àÄ i, @continuous _ _ (t i) _ s,
  { simpa only [continuous_Prop] using this },
  simp only [continuous_iff_le_induced, supr_le_iff]
end

lemma is_closed_infi_iff {s : set Œ±} : @is_closed _ (‚®Ü i, t i) s ‚Üî ‚àÄ i, @is_closed _ (t i) s :=
is_open_supr_iff

end infi
