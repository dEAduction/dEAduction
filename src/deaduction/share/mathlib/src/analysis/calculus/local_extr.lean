/-
Copyright (c) 2019 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import topology.local_extr
import analysis.calculus.deriv

/-!
# Local extrema of smooth functions

## Main definitions

In a real normed space `E` we define `pos_tangent_cone_at (s : set E) (x : E)`.
This would be the same as `tangent_cone_at ‚Ñù‚â•0 s x` if we had a theory of normed semifields.
This set is used in the proof of Fermat's Theorem (see below), and can be used to formalize
[Lagrange multipliers](https://en.wikipedia.org/wiki/Lagrange_multiplier) and/or
[Karush‚ÄìKuhn‚ÄìTucker conditions](https://en.wikipedia.org/wiki/Karush‚ÄìKuhn‚ÄìTucker_conditions).

## Main statements

For each theorem name listed below, we also prove similar theorems for `min`, `extr` (if applicable)`,
and `(f)deriv` instead of `has_fderiv`.

* `is_local_max_on.has_fderiv_within_at_nonpos` : `f' y ‚â§ 0` whenever `a` is a local maximum
  of `f` on `s`, `f` has derivative `f'` at `a` within `s`, and `y` belongs to the positive tangent
  cone of `s` at `a`.

* `is_local_max_on.has_fderiv_within_at_eq_zero` : In the settings of the previous theorem, if both
  `y` and `-y` belong to the positive tangent cone, then `f' y = 0`.

* `is_local_max.has_fderiv_at_eq_zero` :
  [Fermat's Theorem](https://en.wikipedia.org/wiki/Fermat's_theorem_(stationary_points)),
  the derivative of a differentiable function at a local extremum point equals zero.

* `exists_has_deriv_at_eq_zero` :
  [Rolle's Theorem](https://en.wikipedia.org/wiki/Rolle's_theorem): given a function `f` continuous
  on `[a, b]` and differentiable on `(a, b)`, there exists `c ‚àà (a, b)` such that `f' c = 0`.

## Implementation notes

For each mathematical fact we prove several versions of its formalization:

* for maxima and minima;
* using `has_fderiv*`/`has_deriv*` or `fderiv*`/`deriv*`.

For the `fderiv*`/`deriv*` versions we omit the differentiability condition whenever it is possible
due to the fact that `fderiv` and `deriv` are defined to be zero for non-differentiable functions.

## References

* [Fermat's Theorem](https://en.wikipedia.org/wiki/Fermat's_theorem_(stationary_points));
* [Rolle's Theorem](https://en.wikipedia.org/wiki/Rolle's_theorem);
* [Tangent cone](https://en.wikipedia.org/wiki/Tangent_cone);

## Tags

local extremum, Fermat's Theorem, Rolle's Theorem
-/

universes u v

open filter set
open_locale topological_space classical

section vector_space

variables {E : Type u} [normed_group E] [normed_space ‚Ñù E] {f : E ‚Üí ‚Ñù} {a : E}
  {f' : E ‚ÜíL[‚Ñù] ‚Ñù}

/-- "Positive" tangent cone to `s` at `x`; the only difference from `tangent_cone_at`
is that we require `c n ‚Üí ‚àû` instead of `‚à•c n‚à• ‚Üí ‚àû`. One can think about `pos_tangent_cone_at`
as `tangent_cone_at nnreal` but we have no theory of normed semifields yet. -/
def pos_tangent_cone_at (s : set E) (x : E) : set E :=
{y : E | ‚àÉ(c : ‚Ñï ‚Üí ‚Ñù) (d : ‚Ñï ‚Üí E), (‚àÄ·∂† n in at_top, x + d n ‚àà s) ‚àß
  (tendsto c at_top at_top) ‚àß (tendsto (Œªn, c n ‚Ä¢ d n) at_top (ùìù y))}

lemma pos_tangent_cone_at_mono : monotone (Œª s, pos_tangent_cone_at s a) :=
begin
  rintros s t hst y ‚ü®c, d, hd, hc, hcd‚ü©,
  exact ‚ü®c, d, mem_sets_of_superset hd $ Œª h hn, hst hn, hc, hcd‚ü©
end

lemma mem_pos_tangent_cone_at_of_segment_subset {s : set E} {x y : E} (h : segment x y ‚äÜ s) :
  y - x ‚àà pos_tangent_cone_at s x :=
begin
  let c := Œªn:‚Ñï, (2:‚Ñù)^n,
  let d := Œªn:‚Ñï, (c n)‚Åª¬π ‚Ä¢ (y-x),
  refine ‚ü®c, d, filter.univ_mem_sets' (Œªn, h _), _, _‚ü©,
  show x + d n ‚àà segment x y,
  { rw segment_eq_image,
    refine ‚ü®(c n)‚Åª¬π, ‚ü®_, _‚ü©, _‚ü©,
    { rw inv_nonneg, apply pow_nonneg, norm_num },
    { apply inv_le_one, apply one_le_pow_of_one_le, norm_num },
    { simp only [d, sub_smul, smul_sub, one_smul], abel } },
  show tendsto c at_top at_top,
  { exact tendsto_pow_at_top_at_top_of_one_lt one_lt_two },
  show filter.tendsto (Œª (n : ‚Ñï), c n ‚Ä¢ d n) filter.at_top (ùìù (y - x)),
  { have : (Œª (n : ‚Ñï), c n ‚Ä¢ d n) = (Œªn, y - x),
    { ext n,
      simp only [d, smul_smul],
      rw [mul_inv_cancel, one_smul],
      exact pow_ne_zero _ (by norm_num) },
    rw this,
    apply tendsto_const_nhds }
end

lemma pos_tangent_cone_at_univ : pos_tangent_cone_at univ a = univ :=
eq_univ_iff_forall.2
begin
  assume x,
  rw [‚Üê add_sub_cancel x a],
  exact mem_pos_tangent_cone_at_of_segment_subset (subset_univ _)
end

/-- If `f` has a local max on `s` at `a`, `f'` is the derivative of `f` at `a` within `s`, and
`y` belongs to the positive tangent cone of `s` at `a`, then `f' y ‚â§ 0`. -/
lemma is_local_max_on.has_fderiv_within_at_nonpos {s : set E} (h : is_local_max_on f s a)
  (hf : has_fderiv_within_at f f' s a) {y} (hy : y ‚àà pos_tangent_cone_at s a) :
  f' y ‚â§ 0 :=
begin
  rcases hy with ‚ü®c, d, hd, hc, hcd‚ü©,
  have hc' : tendsto (Œª n, ‚à•c n‚à•) at_top at_top,
    from tendsto_at_top_mono _ (Œª n, le_abs_self _) hc,
  refine le_of_tendsto at_top_ne_bot (hf.lim at_top hd hc' hcd) _,
  replace hd : tendsto (Œª n, a + d n) at_top (nhds_within (a + 0) s),
  from tendsto_inf.2 ‚ü®tendsto_const_nhds.add (tangent_cone_at.lim_zero _ hc' hcd),
    by rwa tendsto_principal‚ü©,
  rw [add_zero] at hd,
  replace h : ‚àÄ·∂† n in at_top, f (a + d n) ‚â§ f a, from mem_map.1 (hd h),
  replace hc : ‚àÄ·∂† n in at_top, 0 ‚â§ c n, from mem_map.1 (hc (mem_at_top (0:‚Ñù))),
  filter_upwards [h, hc],
  simp only [mem_set_of_eq, smul_eq_mul, mem_preimage, subset_def],
  assume n hnf hn,
  exact mul_nonpos_of_nonneg_of_nonpos hn (sub_nonpos.2 hnf)
end

/-- If `f` has a local max on `s` at `a` and `y` belongs to the positive tangent cone
of `s` at `a`, then `f' y ‚â§ 0`. -/
lemma is_local_max_on.fderiv_within_nonpos {s : set E} (h : is_local_max_on f s a)
  {y} (hy : y ‚àà pos_tangent_cone_at s a) :
  (fderiv_within ‚Ñù f s a : E ‚Üí ‚Ñù) y ‚â§ 0 :=
if hf : differentiable_within_at ‚Ñù f s a
then h.has_fderiv_within_at_nonpos hf.has_fderiv_within_at hy
else by { rw fderiv_within_zero_of_not_differentiable_within_at hf, refl }

/-- If `f` has a local max on `s` at `a`, `f'` is a derivative of `f` at `a` within `s`, and
both `y` and `-y` belong to the positive tangent cone of `s` at `a`, then `f' y ‚â§ 0`. -/
lemma is_local_max_on.has_fderiv_within_at_eq_zero {s : set E} (h : is_local_max_on f s a)
  (hf : has_fderiv_within_at f f' s a) {y} (hy : y ‚àà pos_tangent_cone_at s a)
  (hy' : -y ‚àà pos_tangent_cone_at s a) :
  f' y = 0 :=
le_antisymm (h.has_fderiv_within_at_nonpos hf hy) $
  by simpa using h.has_fderiv_within_at_nonpos hf hy'

/-- If `f` has a local max on `s` at `a` and both `y` and `-y` belong to the positive tangent cone
of `s` at `a`, then `f' y = 0`. -/
lemma is_local_max_on.fderiv_within_eq_zero {s : set E} (h : is_local_max_on f s a)
  {y} (hy : y ‚àà pos_tangent_cone_at s a) (hy' : -y ‚àà pos_tangent_cone_at s a) :
  (fderiv_within ‚Ñù f s a : E ‚Üí ‚Ñù) y = 0 :=
if hf : differentiable_within_at ‚Ñù f s a
then h.has_fderiv_within_at_eq_zero hf.has_fderiv_within_at hy hy'
else by { rw fderiv_within_zero_of_not_differentiable_within_at hf, refl }

/-- If `f` has a local min on `s` at `a`, `f'` is the derivative of `f` at `a` within `s`, and
`y` belongs to the positive tangent cone of `s` at `a`, then `0 ‚â§ f' y`. -/
lemma is_local_min_on.has_fderiv_within_at_nonneg {s : set E} (h : is_local_min_on f s a)
  (hf : has_fderiv_within_at f f' s a) {y} (hy : y ‚àà pos_tangent_cone_at s a) :
  0 ‚â§ f' y :=
by simpa using h.neg.has_fderiv_within_at_nonpos hf.neg hy

/-- If `f` has a local min on `s` at `a` and `y` belongs to the positive tangent cone
of `s` at `a`, then `0 ‚â§ f' y`. -/
lemma is_local_min_on.fderiv_within_nonneg {s : set E} (h : is_local_min_on f s a)
  {y} (hy : y ‚àà pos_tangent_cone_at s a) :
  (0:‚Ñù) ‚â§ (fderiv_within ‚Ñù f s a : E ‚Üí ‚Ñù) y :=
if hf : differentiable_within_at ‚Ñù f s a
then h.has_fderiv_within_at_nonneg hf.has_fderiv_within_at hy
else by { rw [fderiv_within_zero_of_not_differentiable_within_at hf], refl }

/-- If `f` has a local max on `s` at `a`, `f'` is a derivative of `f` at `a` within `s`, and
both `y` and `-y` belong to the positive tangent cone of `s` at `a`, then `f' y ‚â§ 0`. -/
lemma is_local_min_on.has_fderiv_within_at_eq_zero {s : set E} (h : is_local_min_on f s a)
  (hf : has_fderiv_within_at f f' s a) {y} (hy : y ‚àà pos_tangent_cone_at s a)
  (hy' : -y ‚àà pos_tangent_cone_at s a) :
  f' y = 0 :=
by simpa using h.neg.has_fderiv_within_at_eq_zero hf.neg hy hy'

/-- If `f` has a local min on `s` at `a` and both `y` and `-y` belong to the positive tangent cone
of `s` at `a`, then `f' y = 0`. -/
lemma is_local_min_on.fderiv_within_eq_zero {s : set E} (h : is_local_min_on f s a)
  {y} (hy : y ‚àà pos_tangent_cone_at s a) (hy' : -y ‚àà pos_tangent_cone_at s a) :
  (fderiv_within ‚Ñù f s a : E ‚Üí ‚Ñù) y = 0 :=
if hf : differentiable_within_at ‚Ñù f s a
then h.has_fderiv_within_at_eq_zero hf.has_fderiv_within_at hy hy'
else by { rw fderiv_within_zero_of_not_differentiable_within_at hf, refl }

/-- Fermat's Theorem: the derivative of a function at a local minimum equals zero. -/
lemma is_local_min.has_fderiv_at_eq_zero (h : is_local_min f a) (hf : has_fderiv_at f f' a) :
  f' = 0 :=
begin
  ext y,
  apply (h.on univ).has_fderiv_within_at_eq_zero hf.has_fderiv_within_at;
    rw pos_tangent_cone_at_univ; apply mem_univ
end

/-- Fermat's Theorem: the derivative of a function at a local minimum equals zero. -/
lemma is_local_min.fderiv_eq_zero (h : is_local_min f a) : fderiv ‚Ñù f a = 0 :=
if hf : differentiable_at ‚Ñù f a then h.has_fderiv_at_eq_zero hf.has_fderiv_at
else fderiv_zero_of_not_differentiable_at hf

/-- Fermat's Theorem: the derivative of a function at a local maximum equals zero. -/
lemma is_local_max.has_fderiv_at_eq_zero (h : is_local_max f a) (hf : has_fderiv_at f f' a) :
  f' = 0 :=
neg_eq_zero.1 $ h.neg.has_fderiv_at_eq_zero hf.neg

/-- Fermat's Theorem: the derivative of a function at a local maximum equals zero. -/
lemma is_local_max.fderiv_eq_zero (h : is_local_max f a) : fderiv ‚Ñù f a = 0 :=
if hf : differentiable_at ‚Ñù f a then h.has_fderiv_at_eq_zero hf.has_fderiv_at
else fderiv_zero_of_not_differentiable_at hf

/-- Fermat's Theorem: the derivative of a function at a local extremum equals zero. -/
lemma is_local_extr.has_fderiv_at_eq_zero (h : is_local_extr f a) :
  has_fderiv_at f f' a ‚Üí f' = 0 :=
h.elim is_local_min.has_fderiv_at_eq_zero is_local_max.has_fderiv_at_eq_zero

/-- Fermat's Theorem: the derivative of a function at a local extremum equals zero. -/
lemma is_local_extr.fderiv_eq_zero (h : is_local_extr f a) : fderiv ‚Ñù f a = 0 :=
h.elim is_local_min.fderiv_eq_zero is_local_max.fderiv_eq_zero

end vector_space

section real

variables {f : ‚Ñù ‚Üí ‚Ñù} {f' : ‚Ñù} {a b : ‚Ñù}

/-- Fermat's Theorem: the derivative of a function at a local minimum equals zero. -/
lemma is_local_min.has_deriv_at_eq_zero (h : is_local_min f a) (hf : has_deriv_at f f' a) :
  f' = 0 :=
by simpa using continuous_linear_map.ext_iff.1
  (h.has_fderiv_at_eq_zero (has_deriv_at_iff_has_fderiv_at.1 hf)) 1

/-- Fermat's Theorem: the derivative of a function at a local minimum equals zero. -/
lemma is_local_min.deriv_eq_zero (h : is_local_min f a) : deriv f a = 0 :=
if hf : differentiable_at ‚Ñù f a then h.has_deriv_at_eq_zero hf.has_deriv_at
else deriv_zero_of_not_differentiable_at hf

/-- Fermat's Theorem: the derivative of a function at a local maximum equals zero. -/
lemma is_local_max.has_deriv_at_eq_zero (h : is_local_max f a) (hf : has_deriv_at f f' a) :
  f' = 0 :=
neg_eq_zero.1 $ h.neg.has_deriv_at_eq_zero hf.neg

/-- Fermat's Theorem: the derivative of a function at a local maximum equals zero. -/
lemma is_local_max.deriv_eq_zero (h : is_local_max f a) : deriv f a = 0 :=
if hf : differentiable_at ‚Ñù f a then h.has_deriv_at_eq_zero hf.has_deriv_at
else deriv_zero_of_not_differentiable_at hf

/-- Fermat's Theorem: the derivative of a function at a local extremum equals zero. -/
lemma is_local_extr.has_deriv_at_eq_zero (h : is_local_extr f a) :
  has_deriv_at f f' a ‚Üí f' = 0 :=
h.elim is_local_min.has_deriv_at_eq_zero is_local_max.has_deriv_at_eq_zero

/-- Fermat's Theorem: the derivative of a function at a local extremum equals zero. -/
lemma is_local_extr.deriv_eq_zero (h : is_local_extr f a) : deriv f a = 0 :=
h.elim is_local_min.deriv_eq_zero is_local_max.deriv_eq_zero

end real

section Rolle

variables (f f' : ‚Ñù ‚Üí ‚Ñù) {a b : ‚Ñù} (hab : a < b) (hfc : continuous_on f (Icc a b)) (hfI : f a = f b)

include hab hfc hfI

/-- A continuous function on a closed interval with `f a = f b` takes either its maximum
or its minimum value at a point in the interior of the interval. -/
lemma exists_Ioo_extr_on_Icc : ‚àÉ c ‚àà Ioo a b, is_extr_on f (Icc a b) c :=
begin
  have ne : (Icc a b).nonempty, from nonempty_Icc.2 (le_of_lt hab),
  -- Consider absolute min and max points
  obtain ‚ü®c, cmem, cle‚ü© : ‚àÉ c ‚àà Icc a b, ‚àÄ x ‚àà Icc a b, f c ‚â§ f x,
    from compact_Icc.exists_forall_le ne hfc,
  obtain ‚ü®C, Cmem, Cge‚ü© : ‚àÉ C ‚àà Icc a b, ‚àÄ x ‚àà Icc a b, f x ‚â§ f C,
    from compact_Icc.exists_forall_ge ne hfc,
  by_cases hc : f c = f a,
  { by_cases hC : f C = f a,
    { have : ‚àÄ x ‚àà Icc a b, f x = f a,
        from Œª x hx, le_antisymm (hC ‚ñ∏ Cge x hx) (hc ‚ñ∏ cle x hx),
      -- `f` is a constant, so we can take any point in `Ioo a b`
      rcases dense hab with ‚ü®c', hc'‚ü©,
      refine ‚ü®c', hc', or.inl _‚ü©,
      assume x hx,
      rw [mem_set_of_eq, this x hx, ‚Üê hC],
      exact Cge c' ‚ü®le_of_lt hc'.1, le_of_lt hc'.2‚ü© },
    { refine ‚ü®C, ‚ü®lt_of_le_of_ne Cmem.1 $ mt _ hC, lt_of_le_of_ne Cmem.2 $ mt _ hC‚ü©, or.inr Cge‚ü©,
      exacts [Œª h, by rw h, Œª h, by rw [h, hfI]] } },
  { refine ‚ü®c, ‚ü®lt_of_le_of_ne cmem.1 $ mt _ hc, lt_of_le_of_ne cmem.2 $ mt _ hc‚ü©, or.inl cle‚ü©,
      exacts [Œª h, by rw h, Œª h, by rw [h, hfI]] }
end

/-- A continuous function on a closed interval with `f a = f b` has a local extremum at some
point of the corresponding open interval. -/
lemma exists_local_extr_Ioo : ‚àÉ c ‚àà Ioo a b, is_local_extr f c :=
let ‚ü®c, cmem, hc‚ü© := exists_Ioo_extr_on_Icc f hab hfc hfI
in ‚ü®c, cmem, hc.is_local_extr $ mem_nhds_sets_iff.2 ‚ü®Ioo a b, Ioo_subset_Icc_self, is_open_Ioo, cmem‚ü©‚ü©

/-- Rolle's Theorem `has_deriv_at` version -/
lemma exists_has_deriv_at_eq_zero (hff' : ‚àÄ x ‚àà Ioo a b, has_deriv_at f (f' x) x) :
  ‚àÉ c ‚àà Ioo a b, f' c = 0 :=
let ‚ü®c, cmem, hc‚ü© := exists_local_extr_Ioo f hab hfc hfI in
  ‚ü®c, cmem, hc.has_deriv_at_eq_zero $ hff' c cmem‚ü©

/-- Rolle's Theorem `deriv` version -/
lemma exists_deriv_eq_zero : ‚àÉ c ‚àà Ioo a b, deriv f c = 0 :=
let ‚ü®c, cmem, hc‚ü© := exists_local_extr_Ioo f hab hfc hfI in
  ‚ü®c, cmem, hc.deriv_eq_zero‚ü©

end Rolle
