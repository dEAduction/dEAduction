/-
Copyright (c) 2019 Reid Barton. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Reid Barton, Scott Morrison

Facts about epimorphisms and monomorphisms.

The definitions of `epi` and `mono` are in `category_theory.category`,
since they are used by some lemmas for `iso`, which is used everywhere.
-/
import category_theory.adjunction.basic
import category_theory.opposites

universes v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

namespace category_theory

variables {C : Type u‚ÇÅ} [category.{v‚ÇÅ} C]

section
variables {D : Type u‚ÇÇ} [category.{v‚ÇÇ} D]

lemma left_adjoint_preserves_epi {F : C ‚•§ D} {G : D ‚•§ C} (adj : F ‚ä£ G)
  {X Y : C} {f : X ‚ü∂ Y} (hf : epi f) : epi (F.map f) :=
begin
  constructor,
  intros Z g h H,
  replace H := congr_arg (adj.hom_equiv X Z) H,
  rwa [adj.hom_equiv_naturality_left, adj.hom_equiv_naturality_left,
    cancel_epi, equiv.apply_eq_iff_eq] at H
end

lemma right_adjoint_preserves_mono {F : C ‚•§ D} {G : D ‚•§ C} (adj : F ‚ä£ G)
  {X Y : D} {f : X ‚ü∂ Y} (hf : mono f) : mono (G.map f) :=
begin
  constructor,
  intros Z g h H,
  replace H := congr_arg (adj.hom_equiv Z Y).symm H,
  rwa [adj.hom_equiv_naturality_right_symm, adj.hom_equiv_naturality_right_symm,
    cancel_mono, equiv.apply_eq_iff_eq] at H
end

lemma faithful_reflects_epi (F : C ‚•§ D) [faithful F] {X Y : C} {f : X ‚ü∂ Y}
  (hf : epi (F.map f)) : epi f :=
‚ü®Œª Z g h H, F.map_injective $
  by rw [‚Üêcancel_epi (F.map f), ‚ÜêF.map_comp, ‚ÜêF.map_comp, H]‚ü©

lemma faithful_reflects_mono (F : C ‚•§ D) [faithful F] {X Y : C} {f : X ‚ü∂ Y}
  (hf : mono (F.map f)) : mono f :=
‚ü®Œª Z g h H, F.map_injective $
  by rw [‚Üêcancel_mono (F.map f), ‚ÜêF.map_comp, ‚ÜêF.map_comp, H]‚ü©
end

/--
A split monomorphism is a morphism `f : X ‚ü∂ Y` admitting a retraction `retraction f : Y ‚ü∂ X`
such that `f ‚â´ retraction f = ùüô X`.

Every split monomorphism is a monomorphism.
-/
class split_mono {X Y : C} (f : X ‚ü∂ Y) :=
(retraction : Y ‚ü∂ X)
(id' : f ‚â´ retraction = ùüô X . obviously)

/--
A split epimorphism is a morphism `f : X ‚ü∂ Y` admitting a section `section_ f : Y ‚ü∂ X`
such that `section_ f ‚â´ f = ùüô Y`.
(Note that `section` is a reserved keyword, so we append an underscore.)

Every split epimorphism is an epimorphism.
-/
class split_epi {X Y : C} (f : X ‚ü∂ Y) :=
(section_ : Y ‚ü∂ X)
(id' : section_ ‚â´ f = ùüô Y . obviously)

/-- The chosen retraction of a split monomorphism. -/
def retraction {X Y : C} (f : X ‚ü∂ Y) [split_mono f] : Y ‚ü∂ X := split_mono.retraction.{v‚ÇÅ} f
@[simp, reassoc]
lemma split_mono.id {X Y : C} (f : X ‚ü∂ Y) [split_mono f] : f ‚â´ retraction f = ùüô X :=
split_mono.id'
/-- The retraction of a split monomorphism is itself a split epimorphism. -/
instance retraction_split_epi {X Y : C} (f : X ‚ü∂ Y) [split_mono f] : split_epi (retraction f) :=
{ section_ := f }

/-- A split mono which is epi is an iso. -/
def is_iso_of_epi_of_split_mono {X Y : C} (f : X ‚ü∂ Y) [split_mono f] [epi f] : is_iso f :=
{ inv := retraction f,
  inv_hom_id' := by simp [‚Üê cancel_epi f] }

/--
The chosen section of a split epimorphism.
(Note that `section` is a reserved keyword, so we append an underscore.)
-/
def section_ {X Y : C} (f : X ‚ü∂ Y) [split_epi f] : Y ‚ü∂ X := split_epi.section_.{v‚ÇÅ} f
@[simp, reassoc]
lemma split_epi.id {X Y : C} (f : X ‚ü∂ Y) [split_epi f] : section_ f ‚â´ f = ùüô Y :=
split_epi.id'
/-- The section of a split epimorphism is itself a split monomorphism. -/
instance section_split_mono {X Y : C} (f : X ‚ü∂ Y) [split_epi f] : split_mono (section_ f) :=
{ retraction := f }

/-- A split epi which is mono is an iso. -/
def is_iso_of_mono_of_split_epi {X Y : C} (f : X ‚ü∂ Y) [mono f] [split_epi f] : is_iso f :=
{ inv := section_ f,
  hom_inv_id' := by simp [‚Üê cancel_mono f] }

/-- Every iso is a split mono. -/
@[priority 100]
instance split_mono.of_iso {X Y : C} (f : X ‚ü∂ Y) [is_iso f] : split_mono f :=
{ retraction := inv f }

/-- Every iso is a split epi. -/
@[priority 100]
instance split_epi.of_iso {X Y : C} (f : X ‚ü∂ Y) [is_iso f] : split_epi f :=
{ section_ := inv f }

/-- Every split mono is a mono. -/
@[priority 100]
instance split_mono.mono {X Y : C} (f : X ‚ü∂ Y) [split_mono f] : mono f :=
{ right_cancellation := Œª Z g h w, begin replace w := w =‚â´ retraction f, simpa using w, end }

/-- Every split epi is an epi. -/
@[priority 100]
instance split_epi.epi {X Y : C} (f : X ‚ü∂ Y) [split_epi f] : epi f :=
{ left_cancellation := Œª Z g h w, begin replace w := section_ f ‚â´= w, simpa using w, end }

/-- Every split mono whose retraction is mono is an iso. -/
def is_iso.of_mono_retraction {X Y : C} {f : X ‚ü∂ Y} [split_mono f] [mono $ retraction f]
  : is_iso f :=
{ inv := retraction f,
  inv_hom_id' := (cancel_mono_id $ retraction f).mp (by simp) }

/-- Every split epi whose section is epi is an iso. -/
def is_iso.of_epi_section {X Y : C} {f : X ‚ü∂ Y} [split_epi f] [epi $ section_ f]
  : is_iso f :=
{ inv := section_ f,
  hom_inv_id' := (cancel_epi_id $ section_ f).mp (by simp) }

instance unop_mono_of_epi {A B : C·µí·µñ} (f : A ‚ü∂ B) [epi f] : mono f.unop :=
‚ü®Œª Z g h eq, has_hom.hom.op_inj ((cancel_epi f).1 (has_hom.hom.unop_inj eq))‚ü©

instance unop_epi_of_mono {A B : C·µí·µñ} (f : A ‚ü∂ B) [mono f] : epi f.unop :=
‚ü®Œª Z g h eq, has_hom.hom.op_inj ((cancel_mono f).1 (has_hom.hom.unop_inj eq))‚ü©

instance op_mono_of_epi {A B : C} (f : A ‚ü∂ B) [epi f] : mono f.op :=
‚ü®Œª Z g h eq, has_hom.hom.unop_inj ((cancel_epi f).1 (has_hom.hom.op_inj eq))‚ü©

instance op_epi_of_mono {A B : C} (f : A ‚ü∂ B) [mono f] : epi f.op :=
‚ü®Œª Z g h eq, has_hom.hom.unop_inj ((cancel_mono f).1 (has_hom.hom.op_inj eq))‚ü©

section
variables {D : Type u‚ÇÇ} [category.{v‚ÇÇ} D]

/-- Split monomorphisms are also absolute monomorphisms. -/
instance {X Y : C} (f : X ‚ü∂ Y) [split_mono f] (F : C ‚•§ D) : split_mono (F.map f) :=
{ retraction := F.map (retraction f),
  id' := by { rw [‚Üêfunctor.map_comp, split_mono.id, functor.map_id], } }

/-- Split epimorphisms are also absolute epimorphisms. -/
instance {X Y : C} (f : X ‚ü∂ Y) [split_epi f] (F : C ‚•§ D) : split_epi (F.map f) :=
{ section_ := F.map (section_ f),
  id' := by { rw [‚Üêfunctor.map_comp, split_epi.id, functor.map_id], } }
end

end category_theory
